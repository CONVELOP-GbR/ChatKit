plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

android {
    namespace "com.stfalcon.chatkit"
    compileSdkVersion 35

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName '0.4.8'
        consumerProguardFiles 'proguard.txt'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.stfalcon'
                artifactId = 'chatkit'
                version = '0.4.18'

                pom {
                    name = 'ChatKit'
                    description = 'Android library for chat UI implementation'
                    url = 'https://github.com/CONVELOP-GbR/ChatKit'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'stfalcon'
                            name = 'Stfalcon'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/CONVELOP-GbR/ChatKit.git'
                        developerConnection = 'scm:git:ssh://github.com/CONVELOP-GbR/ChatKit.git'
                        url = 'https://github.com/CONVELOP-GbR/ChatKit/tree/main'
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/CONVELOP-GbR/ChatKit")
                credentials {
                    username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                    password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'

    // Flexbox
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}